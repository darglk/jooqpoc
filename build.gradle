import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import nu.studer.gradle.jooq.JooqEdition

plugins {
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.jetbrains.kotlin.jvm' version '1.8.22'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.8.22'
	id 'nu.studer.jooq' version '8.2'
	id 'org.flywaydb.flyway' version '9.16.3'
	id "org.jetbrains.kotlin.plugin.noarg" version "1.9.20"
}

noArg {
	annotation("com.darglk.jooqpoc.NoArg")
}

group = 'com.darglk'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	flywayMigration
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	runtimeOnly 'org.postgresql:postgresql'
	jooqGenerator 'org.postgresql:postgresql:42.5.1'
	flywayMigration 'org.postgresql:postgresql:42.5.1'
	implementation 'org.jooq:jooq-meta:3.18.7'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

flyway {
	configurations = ['flywayMigration']
	url = 'jdbc:postgresql://localhost:5433/jooqpoc'
	user = 'jooqpoc'
	password = 'jooqpoc'
	schemas = ['jooqpoc']
}

jooq {
	version = '3.18.7'
	edition = JooqEdition.OSS

	configurations {
		main {
			generationTool {
				logging = org.jooq.meta.jaxb.Logging.WARN
				jdbc {
					driver = 'org.postgresql.Driver'
					url = 'jdbc:postgresql://localhost:5433/jooqpoc'
					user = 'jooqpoc'
					password = 'jooqpoc'
					properties {
						property {
							key = 'PAGE_SIZE'
							value = 2048
						}
					}
				}
				generator {
					name = 'org.jooq.codegen.DefaultGenerator'
					database {
						name = 'org.jooq.meta.postgres.PostgresDatabase'
						forcedTypes {
							forcedType {
								name = 'varchar'
								includeExpression = '.*'
								includeTypes = 'JSONB?'
							}
							forcedType {
								name = 'varchar'
								includeExpression = '.*'
								includeTypes = 'INET'
							}
						}
					}
					generate {
						deprecated = false
						records = false
						immutablePojos = false
						fluentSetters = false
					}
					target {
						packageName = 'nu.studer.sample'
						directory = 'src/main/kotlin/entity/jooq'
					}
					strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
				}
			}
		}
	}
}

tasks.withType(KotlinCompile) {
	kotlinOptions {
		freeCompilerArgs += '-Xjsr305=strict'
		jvmTarget = '17'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('generateJooq').configure {
	// ensure database schema has been prepared by Flyway before generating the jOOQ sources
	dependsOn tasks.named('flywayMigrate')

	// declare Flyway migration scripts as inputs on the jOOQ task
	inputs.files(fileTree('src/main/resources/schema/0.0.1'))
			.withPropertyName('migrations')
			.withPathSensitivity(PathSensitivity.RELATIVE)

	// make jOOQ task participate in incremental builds (and build caching)
	allInputsDeclared = true
}